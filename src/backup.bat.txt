Echo off

Rem Automatic Server Backup by WinSCP v1.3.2
Rem
Rem Copyright 2014, Kaoru Ishikura
Rem Released under the GPL Version 3 license.

Rem Backup environment variables.
Rem ##############################################
Rem Specify the path to the config folder.
Set CONF_DIR=%~dp0conf
Rem Specify the path to the WinSCP.exe file.
Set WINSCP_PATH=%~dp0bin\winscp\WinSCP.exe
Rem Specify the timeout message.
Set TIMEOUT_MESSAGE=タイムアウトしました
Rem Specify the path to the 7za.exe file.
Set SEVEN_ZIP_PATH=
Rem Specify whether to enable compression mode(y/n).
Set COMPRESSION_MODE=n
Rem Specify the type of compression(zip/7z).
Set COMPRESSION_TYPE=7z
Rem Specify the level of compression(0/1/3/5/7/9).
Set COMPRESSION_LEVEL=7
Rem ##############################################

Rem Search for saved config files.
For /f %%f In ('Dir /b "%CONF_DIR%\*.ini"') Do (
    Echo --------------------------------------------------
    Echo ファイル名：%%f
    For /f "usebackq eol=; tokens=1,2 delims==" %%a In ("%CONF_DIR%\%%f") Do (
        For %%i In (BACKUP_DIR_PATH REMOTE_DIR_PATH PROTOCOL INVOKING_METHOD PORT_NUMBER HOST_NAME USER_NAME PASSWORD HOST_KEY CERTIFICATE PRIVATE_KEY FILE_MASK MAX_SYNC_RETRIES MAX_BACKUP) Do (
            If /i "%%a"=="%%i" Set %%i=%%b
        )
    )
    Call :main
)

Echo --------------------------------------------------
Echo バックアップが完了しました。
Echo;
Echo;

Pause
Goto :EOF

Rem Start main routine.
:main

Echo 開始時刻：%date% %time%
Echo;

Set BACKUP_DIR_PATH=%BACKUP_DIR_PATH%\
Set BACKUP_DIR_PATH=%BACKUP_DIR_PATH:\\=%

Rem Set the path to the latest folder or the latest zip file.
For /f "tokens=1" %%f In ('Dir /a:-s-h-r /b /o:-d "%BACKUP_DIR_PATH%\*"') Do (
    Set LATEST_FILE_NAME=%%f
    Goto break1
)
:break1

Set LATEST_BACKUP_PATH=%BACKUP_DIR_PATH%\%LATEST_FILE_NAME%

Rem Set the path to the YYYYMMDD folder.
Set NEW_BACKUP_PATH=%BACKUP_DIR_PATH%\%date:~-10,4%%date:~-5,2%%date:~-2%
If Exist "%NEW_BACKUP_PATH%" Rmdir /s /q "%NEW_BACKUP_PATH%"

Rem Set the path to the temporary folder.
Set TEMP_DIR_PATH=%BACKUP_DIR_PATH%\tmp
If Not Exist "%TEMP_DIR_PATH%" Mkdir "%TEMP_DIR_PATH%"

Rem Copy the latest backup to the new folder or unzip.
If Not "%LATEST_FILE_NAME%" == "" (
    For /f %%f In ('Echo %LATEST_FILE_NAME%') Do (
        Set LATEST_FILE_EXTENSION=%%~xf
        Goto break2
    )
:break2
    If Exist "%LATEST_BACKUP_PATH%\" (
        Echo ファイルをコピー中...
        Xcopy "%LATEST_BACKUP_PATH%" "%NEW_BACKUP_PATH%\" /e /q /h /y
        Echo;
    ) Else If Exist "%SEVEN_ZIP_PATH%" (
        If Not "%LATEST_FILE_EXTENSION%" == ".zip" If Not "%LATEST_FILE_EXTENSION%" == ".7z" (
            Goto skip
        )
        Echo ファイルを解凍中...
        "%SEVEN_ZIP_PATH%" x -y "%LATEST_BACKUP_PATH%" -o"%NEW_BACKUP_PATH%">NUL
        If %ERRORLEVEL% == 0 (
            Echo ファイルの解凍が完了しました
        ) Else (
            Echo ファイルの解凍が失敗しました
        )
        Echo;
    )
)
:skip
If Not Exist "%NEW_BACKUP_PATH%" Mkdir "%NEW_BACKUP_PATH%"

Rem Delete ftp log files.
If Exist "%NEW_BACKUP_PATH%\scp*.log" Del /q "%NEW_BACKUP_PATH%\scp*.log"

Rem Set the path to the WinSCP batch file and log files.
Set /a CNT=1
Set SCP_BAT_PATH=%BACKUP_DIR_PATH%\scp.bat
Set SCP_LOG_PATH=%TEMP_DIR_PATH%\scp%CNT%.log

Rem Make the WinSCP batch file.
Echo option echo off>"%SCP_BAT_PATH%"
Echo option batch continue>>"%SCP_BAT_PATH%"
Echo option confirm off>>"%SCP_BAT_PATH%"
Echo option transfer automatic>>"%SCP_BAT_PATH%"
Echo option reconnecttime 15>>"%SCP_BAT_PATH%"

Set SESSION=%PROTOCOL%://%USER_NAME%:%PASSWORD%@%HOST_NAME%:%PORT_NUMBER%
If "%PROTOCOL%" == "sftp" (
    Echo open %SESSION% -hostkey="%HOST_KEY%">>"%SCP_BAT_PATH%"
) Else If "%PROTOCOL%" == "scp" (
    Echo open %SESSION% -privatekey="%PRIVATE_KEY%" -hostkey="%HOST_KEY%">>"%SCP_BAT_PATH%"
) Else If "%PROTOCOL%" == "ftps" (
    If "%INVOKING_METHOD%" == "" Goto ftp
    Echo open %SESSION% -%INVOKING_METHOD% -certificate="%CERTIFICATE%">>"%SCP_BAT_PATH%"
) Else (
:ftp
    Echo open %SESSION%>>"%SCP_BAT_PATH%"
)

Rem Set the file mask parameter.
If Not "%FILE_MASK%" == "" (
    Set FILE_MASK=-filemask="%FILE_MASK%"
)

Echo synchronize local -delete -criteria=time %FILE_MASK% "%NEW_BACKUP_PATH%" "%REMOTE_DIR_PATH%">>"%SCP_BAT_PATH%"

Echo close>>"%SCP_BAT_PATH%"
Echo exit>>"%SCP_BAT_PATH%"

Echo サーバーと同期中...

Rem Run the WinSCP by batch mode.
:sync
"%WINSCP_PATH%" /log="%SCP_LOG_PATH%" /script="%SCP_BAT_PATH%"

Rem If synchronization fails, retry.
Find "%TIMEOUT_MESSAGE%" "%SCP_LOG_PATH%">NUL
If %ERRORLEVEL% == 0 (
    If %MAX_SYNC_RETRIES% Geq %CNT% (
        Echo サーバーとの接続がタイムアウトになりました
        Echo サーバーと再同期中...
        Set /a CNT=%CNT%+1
        Set SCP_LOG_PATH=%TEMP_DIR_PATH%\scp%CNT%.log
        Goto sync
    ) Else (
        Echo サーバーとの同期が失敗しました
    )
) Else (
    Echo サーバーとの同期が完了しました
)
Echo;

Rem Remove temporary files.
If Exist "%SCP_BAT_PATH%" Del /q "%SCP_BAT_PATH%"
If Exist "%TEMP_DIR_PATH%\" (
    Echo ログファイルをコピー中...
    Xcopy "%TEMP_DIR_PATH%" "%NEW_BACKUP_PATH%\" /q /y
    Rmdir /s /q "%TEMP_DIR_PATH%"
    Echo;
)

Rem Compress files.
If Exist "%SEVEN_ZIP_PATH%" If "%COMPRESSION_MODE%" == "y" (
    Echo ファイルを圧縮中...
    If Exist "%NEW_BACKUP_PATH%.%COMPRESSION_TYPE%" (
        Del /q "%NEW_BACKUP_PATH%.%COMPRESSION_TYPE%"
    )
    "%SEVEN_ZIP_PATH%" a -y -mx=%COMPRESSION_LEVEL% ^
    "%NEW_BACKUP_PATH%.%COMPRESSION_TYPE%" "%NEW_BACKUP_PATH%\*">NUL
    If %ERRORLEVEL% == 0 (
        Echo ファイルの圧縮が完了しました
    ) Else (
        Echo ファイルの圧縮が失敗しました
    )
    Rmdir /s /q "%NEW_BACKUP_PATH%"
    Echo;
)

Rem Delete old files.
Echo 古いファイルを削除中...
For /f "skip=%MAX_BACKUP%" %%f In ('Dir /a:-s-h-r /b /o:-d "%BACKUP_DIR_PATH%\*"') Do (
    If Exist "%BACKUP_DIR_PATH%\%%f\" (
        Rmdir /s /q "%BACKUP_DIR_PATH%\%%f"
    ) Else (
        Del /q "%BACKUP_DIR_PATH%\%%f"
    )
)
Echo 古いファイルの削除が完了しました
Echo;
Echo;

Exit /b 1
Rem End main routine.

Echo off

Rem Automatic Server Backup by WinSCP v1.0.0
Rem 
Rem Copyright 2012, Kaoru Ishikura
Rem Licensed under the GPL Version 3 licenses.

Rem Backup environment variables.
Rem ##############################################
Rem Specify the path to the config folder.
Set CONF_DIR=%~dp0conf
Rem Specify the path to the WinSCP.exe file.
Set WINSCP_PATH=%~dp0bin\winscp\WinSCP.exe
Rem Specify the timeout message.
Set TIMEOUT_MESSAGE=タイムアウトしました
Rem Specify the path to the 7za.exe file.
Set SEVEN_ZIP_PATH=
Rem Specify whether to enable compression mode(y/n).
Set COMPRESSION_MODE=n
Rem Specify the compression type(zip/7z).
Set COMPRESSION_TYPE=7z
Rem ##############################################

Rem Search for saved config batch files.
For /f %%f In ('Dir /b "%CONF_DIR%\*.ini"') Do (
    Echo --------------------------------------------------
    Echo ファイル名：%%f
    For /f "usebackq eol=; tokens=1,2 delims==" %%a In ("%CONF_DIR%\%%f") Do (
        For %%i In (BACKUP_DIR_PATH REMOTE_DIR_PATH PROTOCOL PORT_NUMBER HOST_NAME USER_NAME PASSWORD HOST_KEY CERTIFICATE PRIVATE_KEY MAX_SYNC_RETRIES MAX_BACKUP) Do (
            If /i "%%a"=="%%i" Set %%i=%%b
        )
    )
    Call :main
)

Echo --------------------------------------------------
Echo バックアップが完了しました。
Echo;
Echo;

Pause
Goto :EOF

Rem Start main routine.
:main

Echo 開始時刻：%date% %time%
Echo;

Rem Set the path to the latest folder or the latest zip file.
For /f "tokens=1" %%f In ('Dir /a:-s-h-r /b /o:-d "%BACKUP_DIR_PATH%\*"') Do (
    Set LATEST_FILE_NAME=%%f
    Goto break
)
:break

Set LATEST_BACKUP_PATH=%BACKUP_DIR_PATH%\%LATEST_FILE_NAME%

Rem Set the path to the YYYYMMDD folder.
Set NEW_BACKUP_PATH=%BACKUP_DIR_PATH%\%date:~-10,4%%date:~-5,2%%date:~-2%
If Exist "%NEW_BACKUP_PATH%" Rmdir /s /q "%NEW_BACKUP_PATH%"
Mkdir "%NEW_BACKUP_PATH%"

Rem Set the path to the temporary folder.
Set TEMP_DIR_PATH=%BACKUP_DIR_PATH%\tmp
If Not Exist "%TEMP_DIR_PATH%" Mkdir "%TEMP_DIR_PATH%"

Rem Copy the latest backup to the new folder or unzip.
For /f %%f In  ('Echo %LATEST_FILE_NAME%') Do Set LATEST_FILE_EXTENSION=%%~xf
If Exist "%LATEST_BACKUP_PATH%\" (
    Echo ファイルをコピー中...
    Echo;
    Xcopy "%LATEST_BACKUP_PATH%" "%NEW_BACKUP_PATH%\" /e /q /h /y
) Else If Exist "%SEVEN_ZIP_PATH%" (
    If Not "%LATEST_FILE_EXTENSION%" == ".zip" If Not "%LATEST_FILE_EXTENSION%" == ".7z" (
        Goto skip
    )
    Echo ファイルを解凍中...
    Echo;
    "%SEVEN_ZIP_PATH%" x -y "%LATEST_BACKUP_PATH%" -o"%NEW_BACKUP_PATH%">NUL
)
:skip

Rem Delete ftp log files.
Del /q "%NEW_BACKUP_PATH%\scp*.log"

Rem Set the path to the WinSCP batch file and log files.
Set /a CNT=1
Set SCP_BAT_PATH=%BACKUP_DIR_PATH%\scp.bat
Set SCP_LOG_PATH=%TEMP_DIR_PATH%\scp%CNT%.log

Rem Make the WinSCP batch file.
Echo option batch continue>"%SCP_BAT_PATH%"
Echo option confirm off>>"%SCP_BAT_PATH%"
Echo option transfer automatic>>"%SCP_BAT_PATH%"
Echo option reconnecttime 15>>"%SCP_BAT_PATH%"

Set SESSION=%PROTOCOL%://%USER_NAME%:%PASSWORD%@%HOST_NAME%:%PORT_NUMBER%
If "%PROTOCOL%" == "sftp" (
    Echo open %SESSION% -hostkey="%HOST_KEY%">>"%SCP_BAT_PATH%"
) Else If "%PROTOCOL%" == "scp" (
    Echo open %SESSION% -privatekey="%PRIVATE_KEY%" -hostkey="%HOST_KEY%">>"%SCP_BAT_PATH%"
) Else If "%PROTOCOL%" == "ftps" (
    Echo open %SESSION% -implicit -certificate="%CERTIFICATE%">>"%SCP_BAT_PATH%"
) Else (
    Echo open %SESSION%>>"%SCP_BAT_PATH%"
)

Echo synchronize local -delete -criteria=time "%NEW_BACKUP_PATH%" "%REMOTE_DIR_PATH%">>"%SCP_BAT_PATH%"

Echo close>>"%SCP_BAT_PATH%"
Echo exit>>"%SCP_BAT_PATH%"

Echo サーバーと同期中...

Rem Run the WinSCP by batch mode.
:sync
"%WINSCP_PATH%" /console /log="%SCP_LOG_PATH%" /script="%SCP_BAT_PATH%"

Rem If synchronization fails, retry.
Find "%TIMEOUT_MESSAGE%" "%SCP_LOG_PATH%">NUL
If %ERRORLEVEL% == 0 (
    If %MAX_SYNC_RETRIES% Geq %CNT% (
        Echo サーバーとの接続がタイムアウトになりました
        Echo サーバーと再同期中...
        Set /a CNT=%CNT%+1
        Set SCP_LOG_PATH=%TEMP_DIR_PATH%\scp%CNT%.log
        Goto sync
    ) Else (
        Echo サーバーとの同期が失敗しました
    )
) Else (
    Echo サーバーとの同期が成功しました
)
Echo;

Rem Remove temporary files.
If Exist "%SCP_BAT_PATH%" Del /q "%SCP_BAT_PATH%"
If Exist "%TEMP_DIR_PATH%\" (
    Echo ログファイルをコピー中...
    Xcopy "%TEMP_DIR_PATH%" "%NEW_BACKUP_PATH%\" /q /y
    Rmdir /s /q "%TEMP_DIR_PATH%"
    Echo;
)

Rem Compress files.
If Exist "%SEVEN_ZIP_PATH%" If "%COMPRESSION_MODE%" == "y" (
    Echo ファイルを圧縮中...
    If Exist "%NEW_BACKUP_PATH%.%COMPRESSION_TYPE%" (
        Del /q "%NEW_BACKUP_PATH%.%COMPRESSION_TYPE%"
    )
    "%SEVEN_ZIP_PATH%" a -y -mx=9 -mfb=128 ^
    "%NEW_BACKUP_PATH%.%COMPRESSION_TYPE%" "%NEW_BACKUP_PATH%\*">NUL
    Rmdir /s /q "%NEW_BACKUP_PATH%"
    Echo ファイルの圧縮が完了しました
    Echo;
)

Rem Delete old files.
Echo 古いファイルを削除中...
For /f "skip=%MAX_BACKUP%" %%f In ('Dir /a:-s-h-r /b /o:-d "%BACKUP_DIR_PATH%\*"') Do (
    If Exist "%BACKUP_DIR_PATH%\%%f\" (
        Rmdir /s /q "%BACKUP_DIR_PATH%\%%f"
    ) Else (
        Del /q "%BACKUP_DIR_PATH%\%%f"
    )
)
Echo 古いファイルの削除が完了しました
Echo;
Echo;

Exit /b 1
Rem End main routine.
